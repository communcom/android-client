apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'io.fabric'

ext {
    applicationIdSuffix = ""
}

android {
    compileSdkVersion targetsdk
    defaultConfig {
        applicationId "io.golos.cyber_android"
        minSdkVersion minsdk
        targetSdkVersion targetsdk
        versionCode appVesrionCode
        versionName appVersionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
        Properties properties = new Properties()
        properties.load(project.rootProject.file('local.properties').newDataInputStream())
        buildConfigField "String", "AUTH_TEST_PASS", properties.getProperty('authtestpass')

        manifestPlaceholders = [crashlyticsEnabled: "false"]
        signingConfig signingConfigs.debug

        javaCompileOptions {
            annotationProcessorOptions {
                includeCompileClasspath true
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            manifestPlaceholders = [crashlyticsEnabled: "true"]
        }
    }

    flavorDimensions "cyber4j-config-type"
    productFlavors {
        prod {
            dimension "cyber4j-config-type"
            resValue "string", "app_name", "Commun-prod"
        }
        stable {
            dimension "cyber4j-config-type"
            resValue "string", "app_name", "Commun"
        }
        dev {
            dimension "cyber4j-config-type"
            resValue "string", "app_name", "Commun-dev"
            applicationIdSuffix ".dev"
        }
        unstable {
            dimension "cyber4j-config-type"
            resValue "string", "app_name", "Commun-unstable"
            applicationIdSuffix ".unstable"
        }
    }

    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }

    dataBinding {
        enabled = true
    }

    configurations {
        all*.exclude group: 'com.google.guava', module: 'listenablefuture'
    }
    packagingOptions {
        exclude 'META-INF/DEPENDENCIES.txt'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/dependencies.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/LGPL2.1'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/notice.txt'
        exclude 'lib/x86_64/darwin/libscrypt.dylib'
        exclude 'lib/x86_64/freebsd/libscrypt.so'
        exclude 'lib/x86_64/linux/libscrypt.so'
        exclude 'org/bitcoinj/crypto/cacerts'
        exclude 'META-INF/rxjava.properties'
        exclude 'about_files/LICENSE-2.0.txt'
        exclude 'META-INF/eclipse.inf'
        exclude 'plugin.xml'
        exclude 'plugin.properties'
    }
}

buildscript {
    repositories {
        mavenCentral()
    }
}

dependencies {
    /* Our libraries */
    implementation("io.golos.commun4j:commun4j:$commun4jVersion")

    /*Modules*/
    implementation project(':domain')
    implementation project(':data')
    implementation project(':shared_core')
    implementation project(':posts_editor')
    implementation project(':posts_parsing_rendering')

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    implementation "androidx.appcompat:appcompat:$androidXAppcompatVersion"
    implementation "com.google.android.material:material:$materialVersion"
    implementation "androidx.recyclerview:recyclerview:$recyclerViewVersion"
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycleVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutinesVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutinesVersion"
    implementation "androidx.constraintlayout:constraintlayout:$constraintLayoutVersion"
    implementation "com.github.bumptech.glide:glide:$glideVersion"
    implementation "androidx.navigation:navigation-fragment-ktx:$navVersion"
    implementation "androidx.navigation:navigation-ui-ktx:$navVersion"
    implementation "androidx.viewpager2:viewpager2:$viewPager2Version"
    implementation "androidx.paging:paging-runtime:$pagingVersion"
    implementation("com.squareup.moshi:moshi:$moshiVersion")
    implementation "com.github.delight-im:Android-AdvancedWebView:$advancedWebViewVersion"
    implementation "ru.tinkoff.decoro:decoro:$decoroVersion"
    implementation "com.github.hedzr:android-file-chooser:$dirchooserVersion"
    implementation "com.jakewharton.timber:timber:$timberVersion"

    /*Firebase*/
    implementation "com.google.firebase:firebase-core:$firebaseCoreSdkVersion"
    implementation "com.google.firebase:firebase-messaging:$fcmVersion"
    implementation "com.crashlytics.sdk.android:crashlytics:$crashlyticsSdkVersion"

    implementation "com.google.android.gms:play-services-vision:$visionVersion"

    implementation "androidx.legacy:legacy-support-v4:$legacyVersion"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycleVersion"

    /*Protobuf*/
    implementation "com.google.protobuf:protobuf-java:$protobufVersion"

    /*Tests*/
    testImplementation "androidx.lifecycle:lifecycle-extensions:$lifecycleVersion"
    testImplementation "androidx.arch.core:core-testing:$archCoreTesting"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutinesVersion"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutinesVersion"
    testImplementation "junit:junit:$junitVersion"
    testImplementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    testImplementation("com.squareup.moshi:moshi:$moshiVersion")
    androidTestImplementation "androidx.test.ext:junit:$test_runner_version"
    androidTestImplementation "androidx.test.espresso:espresso-core:$espresso_runner_version"

    /* Dagger */
    implementation "com.google.dagger:dagger:$dagger_version"
    kapt "com.google.dagger:dagger-compiler:$dagger_version"

    /* Libraries & modules */
    testImplementation project(':domain')
    testImplementation "io.golos.commun4j:commun4j:$commun4jVersion"
    testImplementation project(':data')
}
